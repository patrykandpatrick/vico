/*
 * Copyright 2022 Patryk Goworowski and Patryk Michalik
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import io.gitlab.arturbosch.detekt.Detekt
import org.jetbrains.dokka.gradle.DokkaMultiModuleTask

buildscript {

    apply from: "versions.gradle"
    apply from: "dependencies.gradle"

    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }

    dependencies {

        classpath "com.android.tools.build:gradle:$versions.agp"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath("org.jetbrains.dokka:dokka-gradle-plugin:$versions.dokka")
        classpath("org.jetbrains.dokka:dokka-base:$versions.dokka")
    }
}

plugins {

    id "io.gitlab.arturbosch.detekt" version "1.20.0"
    id "org.jetbrains.dokka" version "1.6.10"
}

dependencies {

    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:1.19.0"
}

detekt {

    allRules = true
    buildUponDefaultConfig = true
    source = files("sample", "vico/core", "vico/compose", "vico/view", "vico/compose-m3", "vico/compose-m2")
    config = files(".idea/detekt-config.yml")
}

tasks.register("detektFix", Detekt) {

    autoCorrect = true
    allRules = true
    buildUponDefaultConfig = true
    source = files("sample", "vico/core", "vico/compose", "vico/view", "vico/compose-m3", "vico/compose-m2")
}

allprojects {

    repositories {

        google()
        mavenLocal()
        mavenCentral()
    }
}

tasks.withType(Detekt).configureEach {

    jvmTarget = "1.8"

    reports {
        html {
            enabled = true
            destination = file("build/reports/detekt/detekt.html")
        }
    }
}

tasks.withType(DokkaMultiModuleTask).configureEach {
    pluginsMapConfiguration.set(["org.jetbrains.dokka.base.DokkaBase": """{ "customStyleSheets": ["$rootDir/logo-styles.css"] }"""])
}

tasks.dokkaHtmlMultiModule {

    outputDirectory.set(file("docs"))
}

task clean(type: Delete) {

    delete rootProject.buildDir
}

tasks.register("assembleVico") { task ->
    task.dependsOn "clean"
    allprojects.forEach { project ->
        if (project.parent != null && project.parent.name.contains("vico")) {
            task.dependsOn "${project.parent.name}:${project.name}:assembleRelease"
        }
    }
}

project(":vico").tasks.configureEach { subtask ->

    if (subtask.name.contains("publishVicoCorePublication")) {
        subtask.mustRunAfter(":assembleVico")
    }
}
